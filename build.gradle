
buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow', version: '2.0.2'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

def tomcatVersion = '9.0.5'

dependencies {
    compile "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
    compile "org.apache.tomcat:tomcat-jasper:${tomcatVersion}"
    compile "org.apache.tomcat:tomcat-jasper-el:${tomcatVersion}"
    compile "org.apache.tomcat:tomcat-jsp-api:${tomcatVersion}"
}

sourceSets {
    main {
        java.srcDirs = ["src/"]
    }
}

task warbootJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    baseName = 'tomcat-warboot'
    classifier = null
    version = null

    configurations = [project.configurations.compile]

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.DSA'

    manifest {
        attributes 'Main-Class': 'Main'
    }

    from (sourceSets.main.output) {
        include '**'
    }
}

defaultTasks 'warbootJar'
